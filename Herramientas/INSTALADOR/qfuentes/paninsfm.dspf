     A*%%TS  SD  20251008  222849  INF1IVAN    REL-V7R5M0  5770-WDS
     A*  91/08/26  16:06:56    SAHR        REL-R03M00  5728-PW1
     A*-------------------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/REFERENCIA REFER)
     A                                      PRINT
     A                                      CHECK(AB)
     A                                      CA03(03)
     A                                      HELP
     A*  91/08/23  11:13:34    SAHR        REL-R03M00  5728-PW1
     A          R PANINSCA
     A*%%TS  SD  20250505  145534  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      TEXT('Cabecera')
     A                                  1  2'PANINS'
     A            XXIDEN         1S 0O  1 10
     A                                  1 13USER
     A                                  1 26'        HDI SEGUROS        '
     A                                      DSPATR(RI)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2  2'                                 I-
     A                                      nstalador                          -
     A                                               '
     A                                      DSPATR(UL)
     A*  91/08/26  16:06:56    SAHR        REL-R03M00  5728-PW1
     A          R PANINSS1                  SFL
     A*%%TS  SD  20250704  170345  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      TEXT('SUBFILE 1')
     A            XXIN43         1A  H
     A            XXDOMI        35A  H
     A            X1OPCI         1Y 0B  8  2CHGINPDFT
     A                                      TEXT('OPCIONES')
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                      CHECK(ER)
     A N49                                  DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      VALUES(0 1 2 4 5 7 8 9 6 3)
     A            X1DESA        10A  B  8  4DSPATR(PR)
     A            X1COME        50A  O  8 18
     A            X1SECU    R        O  8 15REFFLD(SECU)
     A*  91/08/23  14:04:54    SAHR        REL-R03M00  5728-PW1
     A          R PANINSC1                  SFLCTL(PANINSS1)
     A*%%TS  SD  20251008  181308  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      TEXT('SUBFILE CONTROL 1')
     A                                      CF05
     A                                      CF06
     A                                      CF09
     A                                      CF17
     A                                      ROLLUP(28)
     A                                      ROLLDOWN(27)
     A                                      VLDCMDKEY(90)
     A                                      CF07
     A                                      CF12(12)
     A                                      KEEP
     A                                      BLINK
     A                                      OVERLAY
     A  31                                  SFLDSP
     A  30                                  SFLDSPCTL
     A N30                                  SFLDLT
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A  31                              5 41'8=Cadena'
     A N41                                  DSPATR(ND)
     A                                  6  2'O'
     A                                      DSPATR(RI)
     A                                  6  4'Desa_****'
     A                                      DSPATR(RI)
     A                                  6 18'Comentario...'
     A                                      DSPATR(RI)
     A                                  4  2'5-VerObjetos'
     A                                  6 15'Se'
     A                                      DSPATR(RI)
     A*  91/08/26  09:32:39    SAHR        REL-R03M00  5728-PW1
     A          R PANINS01
     A*%%TS  SD  20250331  143119  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      VLDCMDKEY(90)
     A                                      BLINK
     A                                      OVERLAY
     A                                 13 19'(No existen registros a visualizar-
     A                                      )'
     A*  91/08/26  11:20:53    SAHR        REL-R03M00  5728-PW1
     A          R PANINSP1
     A*%%TS  SD  20250704  155455  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      TEXT('PIE PANTALLA 1')
     A                                      OVERLAY
     A            X1OMSG         7A  H
     A            X1MSF1         6A  O 20 74DSPATR(HI)
     A            XXEMSU        79A  O 21  2DSPATR(UL)
     A                                 22  2'F3=Salir'
     A                                 22 11'F5=Actualizar'
     A  51
     AO 52        X1MSG         78A  O 24  2MSGID(&X1OMSG SINMSG)
     A                                      DSPATR(HI)
     A N51N52                          24 43'(c) COPYRIGHT Colinet-Trotta SRL 1-
     A                                      996'
     A                                      DSPATR(HI)
     A                                 22 25'F6/7=Instalacion Manual/Completa'
     A*         R PANINSS1                  SFL
     A          R PANINSS2                  SFL
     A*%%TS  SD  20250708  145609  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      TEXT('SUBFILE 2')
     A            XXIN43         1A  H
     A            XXDOMI        35A  H
     A            X2OBJE        10A  B  8  7DSPATR(PR)
     A            X2LIBO        10A  B  8 18DSPATR(PR)
     A            X2LIBF        10A  B  8 37DSPATR(PR)
     A            X2SRCL        10A  B  8 48DSPATR(PR)
     A            X2OPCI         1Y 0B  8  4CHGINPDFT
     A                                      TEXT('OPCIONES')
     A                                      DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                      CHECK(ER)
     A N49                                  DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      VALUES(0 1 2 4 5 7 8 9 6 3)
     A            X2ESTO         7A  B  8 29DSPATR(PR)
     A            X2ESTF         7A  B  8 59
     A  33                                  COLOR(RED)
     A                                      DSPATR(PR)
     A*         R PANINSC1                  SFLCTL(PANINSS1)
     A          R PANINSC2                  SFLCTL(PANINSS2)
     A*%%TS  SD  20251008  221701  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      TEXT('SUBFILE CONTROL 1')
     A                                      CF05
     A                                      CF06
     A                                      CF09
     A                                      CF17
     A                                      ROLLUP(27)
     A                                      ROLLDOWN(28)
     A                                      VLDCMDKEY(90)
     A                                      CF12(12)
     A                                      CF07(07)
     A                                      CF08(08)
     A                                      KEEP
     A                                      BLINK
     A                                      OVERLAY
     A  31                                  SFLDSP
     A  30                                  SFLDSPCTL
     A N30                                  SFLDLT
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A  31                              5 41'8=Cadena'
     A N41                                  DSPATR(ND)
     A                                  6  7'Objetos...'
     A                                      DSPATR(RI)
     A                                  6 18'Lib Objeto'
     A                                      DSPATR(RI)
     A                                  6 37'Lib Fuente'
     A                                      DSPATR(RI)
     A                                  6 48'Src Fuente'
     A                                      DSPATR(RI)
     A                                  5  5'Desa:'
     A            X2DESA        10A  B  5 11DSPATR(PR)
     A                                  6  4'OP'
     A                                      DSPATR(RI)
     A                                  6 29'Est/Obj'
     A                                      DSPATR(RI)
     A                                  6 59'Est/Fue'
     A                                      DSPATR(RI)
     A                                  4  5'Opc 2 = Modificar destino de obj/F-
     A                                      uente'
     A          R FUENTEAERR
     A*%%TS  SD  20251008  215155  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      WINDOW(2 25 2 25)
     A                                  1  2'Error en Fuentes '
     A          R PANINSAUT
     A*%%TS  SD  20250704  151700  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      WINDOW(4 15 5 60)
     A                                      CF01
     A                                  1  2'Instalacion'
     A                                  3  2'Desa..:'
     A                                  4  2'Descr.:'
     A            XXDESC        50A  I  4 10
     A            XXDESA        10A  B  3 10
     A            TIPOIN        10A  O  1 14
     A          R ERROBJLCK
     A*%%TS  SD  20250528  125704  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      WINDOW(2 25 2 25)
     A                                  1  1'Objeto Tomado:'
     A            ERROBJ        10A  O  1 16
     A          R ERROBCPP
     A*%%TS  SD  20250528  131150  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      WINDOW(2 25 7 25)
     A                                  1  1'Algo fallo en el restor'
     A                                  2  1'Puede que el srcpf no '
     A                                  3  1'tenga el mismo formato'
     A                                  4  1'que el savf'
     A                                  4 13'o'
     A                                  4 15'que'
     A                                  4 19'el'
     A                                  5  1'nombre'
     A                                  5  8'del'
     A                                  5 12'savf'
     A                                  5 17'este'
     A                                  5 22'mal'
     A          R CANCELA
     A*%%TS  SD  20250528  131506  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      WINDOW(2 25 2 25)
     A                                  1  1'Se cancela la instalacion'
     A          R TODOOK
     A*%%TS  SD  20250704  132157  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      WINDOW(2 25 2 25)
     A                                  1  1'Se instao correctamente! '
     A          R PANCHG01
     A*%%TS  SD  20251008  222849  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      WINDOW(2 25 15 35)
     A                                  1  2'Cambio de destinos de obj y/o '
     A                                  2  2'fuentes'
     A                                  4  2'Objeto/fuente:'
     A                                      DSPATR(UL)
     A                                  6  2'Destino Fuente Actual'
     A                                      DSPATR(UL)
     A                                  8  2'Destino Objeto Actual'
     A                                      DSPATR(UL)
     A            XXOBJF        10A  O  5  2COLOR(BLU)
     A            XXDFAL        10A  O  7  2COLOR(BLU)
     A            XXDOBA        10A  O  9  2COLOR(BLU)
     A                                 10  1'-----------------------------------
     A                                      -'
     A                                 11  2'Nuevo destino Fuente:'
     A                                      DSPATR(UL)
     A            XXDFNL        10A  B 12  2
     A  34                                  DSPATR(PR)
     A                                      COLOR(BLU)
     A                                 13  2'Nuevo destino Objeto:'
     A                                      DSPATR(UL)
     A            XXDOBN        10A  B 14  2
     A  33                                  DSPATR(PR)
     A                                      COLOR(BLU)
     A                                  3  1'-----------------------------------
     A                                      -'
     A            XXDFAS        10A  O  7 15COLOR(BLU)
     A                                  7 13'/'
     A                                 12 13'/'
     A            XXDFNS        10A  B 12 15
     A  34                                  DSPATR(PR)
     A                                      COLOR(BLU)
     A            XXOBJI        10A  O 14 14
     A N33                                  DSPATR(ND)
     A                                      COLOR(RED)
     A            XXFUEI        10A  O 12 26
     A N34                                  DSPATR(ND)
     A                                      COLOR(RED)
     A*         R PANINSP1
     A          R PANINSP2
     A*%%TS  SD  20250708  150159  INF1IVAN    REL-V7R5M0  5770-WDS
     A                                      TEXT('PIE PANTALLA 1')
     A                                      OVERLAY
     A            X1OMSG         7A  H
     A            X1MSF1         6A  O 20 74DSPATR(HI)
     A            XXEMSU        79A  O 21  2DSPATR(UL)
     A                                 22  2'F3=Salir'
     A  51
     AO 52        X1MSG         78A  O 24  2MSGID(&X1OMSG SINMSG)
     A                                      DSPATR(HI)
     A N51N52                          24 43'(c) COPYRIGHT Colinet-Trotta SRL 1-
     A                                      996'
     A                                      DSPATR(HI)
     A                                 22 11'F7/8=Instalacion Fuentes/Objetos'
