PGM PARM(&FROMFILE &TOFILE &MBR)

    DCL VAR(&FROMFILE) TYPE(*CHAR) LEN(20)
    DCL VAR(&TOFILE) TYPE(*CHAR) LEN(20)
    DCL VAR(&MBR) TYPE(*CHAR) LEN(10)
    DCL VAR(&FFILE) TYPE(*CHAR) LEN(10)
    DCL VAR(&FLIBR) TYPE(*CHAR) LEN(10)
    DCL VAR(&TFILE) TYPE(*CHAR) LEN(10)
    DCL VAR(&TLIBR) TYPE(*CHAR) LEN(10)
    DCL VAR(&RTNLIB) TYPE(*CHAR) LEN(10)
    DCL VAR(&RTNLI2) TYPE(*CHAR) LEN(10)
    DCL VAR(&NEW) TYPE(*CHAR) LEN(10)
    DCL VAR(&NEWM) TYPE(*CHAR) LEN(10)

    CHGVAR VAR(&FFILE) VALUE(%SST(&FROMFILE 01 10))
    CHGVAR VAR(&FLIBR) VALUE(%SST(&FROMFILE 11 10))
    CHGVAR VAR(&TFILE) VALUE(%SST(&TOFILE 01 10))
    CHGVAR VAR(&TLIBR) VALUE(%SST(&TOFILE 11 10))

    RTVOBJD OBJ(&TLIBR/&TFILE) OBJTYPE(*FILE) RTNLIB(&RTNLIB)
    MONMSG MSGID(CPF9801 CPF9815 CPF9812) EXEC(DO)
       SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('No +
                    existe el archivo ' || &TFILE |< ' en ' +
                    || &TLIBR) MSGTYPE(*ESCAPE)
                    RETURN
    ENDDO

    RTVOBJD OBJ(&FLIBR/&FFILE) OBJTYPE(*FILE) RTNLIB(&RTNLI2)
    MONMSG MSGID(CPF9801 CPF9815 CPF9812) EXEC(DO)
       SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('No +
                    existe el archivo ' || &FFILE |< ' en ' +
                    || &FLIBR) MSGTYPE(*ESCAPE)
                    RETURN
    ENDDO

    CHKOBJ OBJ(&RTNLI2/&FFILE) OBJTYPE(*FILE) MBR(&MBR)
    MONMSG MSGID(CPF9801 CPF9815 CPF9812) EXEC(DO)
      SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('No +
                   existe el miembro ' || &MBR || ' en el +
                   archivo ' || &FFILE |< ' en ' || +
                   &RTNLI2) MSGTYPE(*ESCAPE)
      RETURN
    ENDDO

TRES:
    CHGVAR VAR(&NEWM) VALUE(&MBR |< '___')
    CHKOBJ OBJ(&RTNLIB/&TFILE) OBJTYPE(*FILE) MBR(&NEWM)
    MONMSG MSGID(CPF9815) EXEC(GOTO CMDLBL(DOS))
    RMVM FILE(&RTNLIB/&TFILE) MBR(&NEWM)

DOS:
    CHGVAR VAR(&NEWM) VALUE(&MBR |< '__')
    CHKOBJ OBJ(&RTNLIB/&TFILE) OBJTYPE(*FILE) MBR(&NEWM)
    MONMSG MSGID(CPF9815) EXEC(GOTO CMDLBL(UNO))
    CHGVAR VAR(&NEW) VALUE(&MBR |< '___')
    RNMM FILE(&RTNLIB/&TFILE) MBR(&NEWM) NEWMBR(&NEW)

UNO:
    CHGVAR VAR(&NEWM) VALUE(&MBR |< '_')
    CHKOBJ OBJ(&RTNLIB/&TFILE) OBJTYPE(*FILE) MBR(&NEWM)
    MONMSG MSGID(CPF9815) EXEC(GOTO CMDLBL(CERO))
    CHGVAR VAR(&NEW) VALUE(&MBR |< '__')
    RNMM FILE(&RTNLIB/&TFILE) MBR(&NEWM) NEWMBR(&NEW)

CERO:
    CHGVAR VAR(&NEWM) VALUE(&MBR)
    CHKOBJ OBJ(&RTNLIB/&TFILE) OBJTYPE(*FILE) MBR(&NEWM)
    MONMSG MSGID(CPF9815) EXEC(GOTO CMDLBL(MUEVE))
    CHGVAR VAR(&NEW) VALUE(&MBR |< '_')
    RNMM FILE(&RTNLIB/&TFILE) MBR(&NEWM) NEWMBR(&NEW)

MUEVE:
    MOVM FROMFILE(&RTNLI2/&FFILE) +
         TOFILE(&RTNLIB/&TFILE) FROMMBR(&MBR)

ENDPGM
